#!/usr/bin/env perl

use 5.012_001;
use strict;
use warnings;
use feature qw(say);
our $VERSION = "0.01";

use App::findeps;
use Getopt::Long;
my $usage = "Usage: $0 FILE | cpanm\n";

my %opts;
GetOptions( \%opts, "l|latest", ) or die $usage;

my $install_latest = $opts{l} || 0;

die $usage unless @ARGV;

my @files = @ARGV;
my $map   = App::findeps::scan( files => \@files );

my @list = ();
foreach my $key ( sort keys %$map ) {
    next if $key =~ /\.pl$/;
    my $version = $map->{$key};
    my $name    = _name($key);
    my $dist    = $name;
    warn $dist .= '~' . $version if $version and $version ne 'required';
    push @list, $dist;
}

die "Ready to run @ARGV\n" unless @list;

say $_ for @list;

exit;

sub _name {
    my $str = shift;
    $str =~ s!/!::!g;
    $str =~ s!.pm$!!i;
    $str =~ s!^auto::(.+)::.*!$1!;
    return $str;
}

__END__

=encoding utf-8

=head1 NAME

findeps - A command-line tool that resolves dependencies from modules

=head1 SYNOPSIS

just run like below:

 $ findeps your_product.pl | cpanm
 $ findeps Plack.psgi | cpanm
 $ findeps index.cgi | cpanm
 $ findeps t/00_compile.t | cpanm

Now you can run the product you've made with many modules
without installing them every time

=head1 DESCRIPTION

findeps is a simple command line program for ready to run.

L<scandeps.pl> requires you to have L<CPANPLUS>, but this requires just only L<cpanm>.

=head1 LICENSE

Copyright (C) worthmine.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 AUTHOR

Yuki Yoshida(L<worthmine|https://github.com/worthmine>)

=cut
